# Copyright Amazon.com, Inc. or its affiliates. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
#
variable "region" {
  description = "AWS region to deploy resources"
  type        = string
  default     = "us-east-1"
}

variable "prefix" {
  description = "Prefix to add to resource names"
  type        = string
  default     = "idp"
}

# User Identity Configuration
variable "admin_email" {
  description = "Optional email address for the admin user. If provided, an admin user will be created in the Cognito User Pool."
  type        = string
  default     = null
}

variable "log_level" {
  description = "The log level for the document processing components"
  type        = string
  default     = "INFO"
  validation {
    condition     = contains(["DEBUG", "INFO", "WARNING", "ERROR", "CRITICAL"], var.log_level)
    error_message = "Allowed values for log_level are \"DEBUG\", \"INFO\", \"WARNING\", \"ERROR\", or \"CRITICAL\"."
  }
}

variable "log_retention_days" {
  description = "The retention period for CloudWatch logs generated by the document processing components in days"
  type        = number
  default     = 7
  validation {
    condition     = contains([1, 3, 5, 7, 14, 30, 60, 90, 120, 150, 180, 365, 400, 545, 731, 1827, 3653], var.log_retention_days)
    error_message = "Log retention days must be one of the allowed values."
  }
}

variable "data_tracking_retention_days" {
  description = "The retention period for document tracking data in days"
  type        = number
  default     = 365
}

# API Configuration (Consolidated)
variable "api" {
  description = "Configuration for GraphQL API and all API-related features"
  type = object({
    # Core API configuration
    enabled = optional(bool, true)

    # Agent Analytics (GraphQL resolvers for agent functionality)
    agent_analytics = optional(object({
      enabled  = optional(bool, false)
      model_id = optional(string, "us.anthropic.claude-3-5-sonnet-20241022-v2:0")
    }), { enabled = false })

    # Discovery (Document discovery and classification workflow)
    discovery = optional(object({
      enabled = optional(bool, false)
    }), { enabled = false })

    # Chat with Document (Document Q&A using Bedrock and Knowledge Base)
    chat_with_document = optional(object({
      enabled                  = optional(bool, false)
      guardrail_id_and_version = optional(string, null)
    }), { enabled = false })

    # Process Changes (Document editing and reprocessing)
    process_changes = optional(object({
      enabled = optional(bool, false)
    }), { enabled = false })

    # Knowledge Base (external dependency for chat feature)
    knowledge_base = optional(object({
      enabled            = optional(bool, false)
      knowledge_base_arn = optional(string)
      model_id           = optional(string, "us.amazon.nova-pro-v1:0")
      embedding_model_id = optional(string, "amazon.titan-embed-text-v2:0")
    }), { enabled = false })
  })

  default = {
    enabled            = true
    agent_analytics    = { enabled = false }
    discovery          = { enabled = false }
    chat_with_document = { enabled = false }
    process_changes    = { enabled = false }
    knowledge_base     = { enabled = true } # Enable by default for BDA example
  }

  validation {
    condition     = !var.api.chat_with_document.enabled || var.api.knowledge_base.enabled
    error_message = "When api.chat_with_document.enabled is true, api.knowledge_base.enabled must also be true."
  }

  validation {
    condition     = !var.api.agent_analytics.enabled || var.api.agent_analytics.model_id != null
    error_message = "When api.agent_analytics.enabled is true, model_id must be provided."
  }
}

# DEPRECATED: Individual API feature variables (use 'api' variable instead)
# These are kept for backward compatibility and will be removed in a future version

variable "web_ui" {
  description = "Web UI configuration object"
  type = object({
    enabled                    = optional(bool, true)
    create_infrastructure      = optional(bool, true)
    bucket_name                = optional(string, null)
    cloudfront_distribution_id = optional(string, null)
    logging_enabled            = optional(bool, false)
    logging_bucket_arn         = optional(string, null)
    enable_signup              = optional(string, "")
  })
  default = {
    enabled                    = true
    create_infrastructure      = true
    bucket_name                = null
    cloudfront_distribution_id = null
    logging_enabled            = false
    logging_bucket_arn         = null
    enable_signup              = ""
  }
}

# Summarization is now configured within the processor configuration object

# BDA Processor Configuration (flat variables for example)
variable "summarization_enabled" {
  description = "Enable document summarization for BDA processor"
  type        = bool
  default     = true
}

variable "summarization_model_id" {
  description = "Model ID for document summarization (BDA processor). If null, uses the default from YAML configuration."
  type        = string
  default     = null
}

# Evaluation Configuration
variable "enable_evaluation" {
  description = "Enable evaluation functionality (simplified flag)"
  type        = bool
  default     = false
}

variable "evaluation_model_id" {
  description = "Model ID for evaluation processing"
  type        = string
  default     = "anthropic.claude-3-sonnet-20240229-v1:0"
}

# Reporting Configuration
variable "enable_reporting" {
  description = "Enable reporting functionality (simplified flag)"
  type        = bool
  default     = false
}

# DEPRECATED: Individual API feature variables (use 'api' variable instead)
# These are kept for backward compatibility and will be removed in a future version
variable "enable_api" {
  description = "DEPRECATED: Use api.enabled instead. Enable GraphQL API for programmatic access and notifications"
  type        = bool
  default     = null
}

variable "agent_analytics" {
  description = "DEPRECATED: Use api.agent_analytics instead. Configuration for agent analytics functionality"
  type = object({
    enabled  = optional(bool, false)
    model_id = optional(string, "us.anthropic.claude-3-5-sonnet-20241022-v2:0")
  })
  default = null
}

variable "discovery" {
  description = "DEPRECATED: Use api.discovery instead. Configuration for document discovery functionality"
  type = object({
    enabled = optional(bool, false)
  })
  default = null
}

variable "chat_with_document" {
  description = "DEPRECATED: Use api.chat_with_document instead. Configuration for chat with document functionality"
  type = object({
    enabled                  = optional(bool, false)
    guardrail_id_and_version = optional(string, null)
  })
  default = null
}

variable "process_changes" {
  description = "DEPRECATED: Use api.process_changes instead. Configuration for document editing and reprocessing functionality"
  type = object({
    enabled = optional(bool, false)
  })
  default = null
}

# Configuration File Path
variable "config_file_path" {
  description = "Path to the configuration YAML file for document processing"
  type        = string
  default     = "../../sources/config_library/pattern-1/lending-package-sample/config.yaml"
}

# DEPRECATED: Knowledge Base variables (use 'api.knowledge_base' instead)
variable "enable_knowledge_base" {
  description = "DEPRECATED: Use api.knowledge_base.enabled instead. Enable AWS Bedrock Knowledge Base for document querying"
  type        = bool
  default     = null
}

variable "knowledge_base_model_id" {
  description = "DEPRECATED: Use api.knowledge_base.model_id instead. Model ID for knowledge base queries"
  type        = string
  default     = null
}

variable "knowledge_base_embeddings_model_id" {
  description = "DEPRECATED: Use api.knowledge_base.embedding_model_id instead. Model ID for knowledge base embeddings"
  type        = string
  default     = null
}

variable "tags" {
  description = "Tags to apply to all resources"
  type        = map(string)
  default     = {}
}

variable "force_layer_rebuild" {
  description = "Force rebuild of Lambda layers regardless of requirements changes"
  type        = bool
  default     = false
}
